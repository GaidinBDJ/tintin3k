#NOP MIP handler version 1.07;
#NOP Original v1.05 by Balthus.;
#NOP v1.06, v1.06 - Bux fixes by Gaidin.;

#NOP ***** Set MIP ID Code **********;
#VAR {mip} {};
#VAR {mip[Version]} {0.107};
#VAR {mip[id]} {};
#LOOP {1} {5} {i}
{
  #MATH {mip[digit]} {1d10 - 1};
  #VAR {mip[id]} {$mip[id]$mip[digit]};
};
#UNVAR {mip[digit]};

#NOP ***** Kick Off MIP *************;
#SEND {3klient $mip[id]~~Tin$mip[Version]};
#SEND {3klient LINEFEED on};
#SEND {3klient HAA on};
#SEND {3klient HAB off};

#NOP ***** Extract Raw Data *********;
#ACTION {~{\#K\%$mip[id](.{3})(.{3})(.*)$}}
{
  #LINE {STRIP} {#VAR {mip[%3][data]} {%4};};
  #REPLACE {mip[%3][data]} {"} {'};
  .aMipProcessFlag {%3} {$mip[%3][data]};
  #IF {"$mip[debug]" == "1"}
  {
    #ECHO {MIP:%3:$mip[%3][data]};
  };
  #LINE GAG;
} {1};

#NOP ***** Individual Data Sections *****;
#ALIAS {.aMipProcessFlag}
{
  #NOP %1;
  #nop Storing argument in variable to fix a bug when glines include a semicolon;
  #var tmp[mipdata] {%2};
  #replace {tmp[mipdata]} {;} {:};
  #SWITCH {"%1"}
  {
    #CASE {"AAA"} {#NOP Sound;};
    #CASE {"AAB"} {#NOP Image;};
    #CASE {"AAC"} {#NOP Reboot Time;#VAR {session[RebootTime]} {$tmp[mipdata]};};
    #CASE {"AAD"} {#NOP Music;};
    #CASE {"AAF"} {#NOP Uptime;#VAR {session[UpTime]} {$tmp[mipdata]};};
    #CASE {"AAG"} {#NOP AVI movie;};
    #CASE {"AAH"} {#NOP Download Media;};
    #CASE {"BAA"} {#NOP Special Textstring;};
    #CASE {"BAB"} {#NOP 2 Way Communications;#VAR {mip[%1][data]} {@fMipTildaConv{$mip[%1][data]}};};
    #CASE {"BAC"} {#NOP Special Textstring2;};
    #CASE {"BAD"} {#NOP Room Description;};
    #CASE {"BAE"} {#NOP Mud Lag;#VAR {session[Lag]} {$tmp[mipdata]};};
    #CASE {"BAF"} {#NOP Send Edit;};
    #CASE {"BBA"} {#NOP Guild Point1 Mask;#VAR {my[gp1][name]} {$tmp[mipdata]};};
    #CASE {"BBB"} {#NOP Guild Point2 Mask;#VAR {my[gp2][name]} {$tmp[mipdata]};};
    #CASE {"BBC"} {#NOP Hit Point Mask;#VAR {my[hp][name]} {%1};};
    #CASE {"BBD"} {#NOP Spell Point Mask;#VAR {my[sp][name]} {%1};};
    #CASE {"CAA"} {#NOP Chat Messages;#VAR {mip[%1][data]} {@fMipTildaConv{$mip[%1][data]}};};
    #CASE {"CAP"} {#NOP Window Caption Text;#FORMAT {temp} {%h} {$tmp[mipdata]};};
    #CASE {"CCF"} {#NOP Send File Line;};
    #CASE {"CDF"} {#NOP Send File Begin;};
    #CASE {"CEF"} {#NOP Send File End;};
    #CASE {"DDD"} {#NOP Room Exits;#REPLACE {mip[DDD][data]} {~} {;};};
    #CASE {"FFF"} {#NOP Combined Stats Data;.aMipProcessFFF {$tmp[mipdata]};};
    #CASE {"HAA"} {#NOP Room Items;#VAR {mip[%1][data]} {@fMipTildaConv{$mip[%1][data]}};};
    #CASE {"HAB"} {#NOP Item Actions;#VAR {mip[%1][data]} {@fMipTildaConv{$mip[%1][data]}};};
    #DEFAULT {#NOP Every other non-handled flag;};
  };
  event_raise {e_mip_%1} {$mip[%1][data]};
  #VAR {mip[last]} {%1}; 
};

#NOP ***** Combined Stats Data *****;
#ALIAS {.aMipProcessFFF}
{
  #NOP %1;
  #REPLACE {mip[FFF][data]} {;} {:};
  #REPLACE {mip[FFF][data]} {~} {;};
  #VAR {temp[toggle]} {-1};
  #FOREACH {$mip[FFF][data]} {temp[item]}
  {
    #MATH {temp[toggle]} {$temp[toggle] * -1};
    #IF {$temp[toggle] > 0}
    {
      #VAR {temp[flag]} {$temp[item]};
    }
    {
      #IF {"$temp[flag]" != ""}
      {
        #VAR {mip[FFF][$temp[flag]][data]} {$temp[item]};
        .aMipProcessFFFSub {$temp[flag]} {$mip[FFF][$temp[flag]][data]};
      };
    };
  };
};

#NOP ***** Separated Stats Data *****;
#ALIAS {.aMipProcessFFFSub}
{
  #NOP %1;
  #SWITCH {"%1"}
  {
    #CASE {"A"} {#NOP Hit Points;#FORMAT {my[hp][current]} {%d} {%2};};
    #CASE {"B"} {#NOP Hit Points Maximum;#FORMAT {my[hp][max]} {%d} {%2};};
    #CASE {"C"} {#NOP Spell Points;#FORMAT {my[sp][current]} {%d} {%2};};
    #CASE {"D"} {#NOP Spell Points Maximum;#FORMAT {my[sp][max]} {%d} {%2};};
    #CASE {"E"} {#NOP Guild Points1;#FORMAT {my[gp1][current]} {%d} {%2};};
    #CASE {"F"} {#NOP Guild Points1 Maximum;#FORMAT {my[gp1][max]} {%d} {%2};};
    #CASE {"G"} {#NOP Guild Points2;#FORMAT {my[gp2][current]} {%d} {%2};};
    #CASE {"H"} {#NOP Guild Points2 Maximum;#FORMAT {my[gp2][max]} {%d} {%2};};
    #CASE {"I"} {#NOP Primary Guild Line;.aMipProcessGline {1} {%2};};
    #CASE {"J"} {#NOP Secondary Guild Line;.aMipProcessGline {2} {%2};};
    #CASE {"K"} {#NOP Mob Fighting;.aMipProcessFFFK {%2};};
    #CASE {"L"} {#NOP Mob Health;#FORMAT {opponent[hp]} {%d} {%2};};
    #CASE {"M"} {#NOP Mob Image File;};
    #CASE {"N"} {#NOP Combat Round Counter;};
    #DEFAULT {#NOP Every other non-handled flag;};
  };
  event_raise {e_mip_FFF_%1} {%2};
};

#NOP ***** Combat Mob Name *****;
#ALIAS {.aMipProcessFFFK}
{
  #NOP %1;
  #IF {"%1" != ""}
  {
    #VAR {opponent} {};
    #VAR {opponent[longname]} {%1};
  }
  {
    #VAR {opponent[hp]} {};
  };
};

#NOP ***** Guild Stats Line 1/2 *****;
#ALIAS {.aMipProcessGline}
{
  #NOP %1;
  #IF {"%2" != ""}
  {
    #VAR {mip[gline][%1]} {@fMipColourConv{{%2}}};
    event_raise e_mip_gline {%1} {$mip[gline][%1]};
  };
};

#NOP ***** Mip Debug *****;
#ALIAS {.aMipDebug}
{
  #NOP %1;
  #IF {"$mip[debug]" == "1"}
  {
    #ECHO {MIP Debugging OFF};
    #VAR {mip[debug]} {0}
  }
  {
    #ECHO {MIP Debugging ON};
    #VAR {mip[debug]} {1}
  };
};

#NOP ***** Guild Stats Line Colour Handling *****;
#FUNCTION {fMipColourConv}
{
  #VAR {result} {%1};
  #REPLACE {result} {>} {<898>};
  #REPLACE {result} {<y} {<838>};
  #REPLACE {result} {<r} {<818>};
  #REPLACE {result} {<b} {<848>};
  #REPLACE {result} {<g} {<828>};
  #REPLACE {result} {<c} {<868>};
  #REPLACE {result} {<v} {<858>};
  #REPLACE {result} {<s} {<278>};
	#REPLACE {result} {<w} {};
};

#NOP ***** Convert Tilda Delimited Strings *****;
#FUNCTION {fMipTildaConv}
{
  #VAR {result} {%1};
  #REPLACE {result} {~} {;};
  #REPLACE {result} {{\^\^}} {~};
};
